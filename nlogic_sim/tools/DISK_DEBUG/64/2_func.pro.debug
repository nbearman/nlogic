-- -- -- -- |	@@FUNC
-- -- -- -- |	/////////////////////////////////////////////////
-- -- -- -- |	//Call target function with new stack frame
-- -- -- -- |	//Processor state is preserved and restored
-- -- -- -- |	//before returning to caller
-- -- -- -- |	
-- -- -- -- |	//push target function arguments onto stack
-- -- -- -- |	//push target function address onto stack
-- -- -- -- |	//load return address into LINK
-- -- -- -- |	//jump to FUNC
-- -- -- -- |	//WMEM accessor is reserved for stack operations
-- -- -- -- |	//DMEM will be overwritten
-- -- -- -- |	//target function address will be overwritten with
-- -- -- -- |	//function call result
-- -- -- -- |	
-- -- -- -- |	//target function should store result at (FP - 0x54 (84))
-- -- -- -- |	//first function argument is accessible at (FP - 0x58 (88))
-- -- -- -- |	
-- -- -- -- |	//invoke this helper with:
-- -- -- -- |	//RTRN LINK
-- -- -- -- |	//IADN PC
-- -- -- -- |	//::FUNC
-- -- -- -- |	/////////////////////////////////////////////////
-- -- -- -- |	//caller save layout
-- -- -- -- |	//GPA
-- -- -- -- |	//GPB
-- -- -- -- |	//GPC
-- -- -- -- |	//GPD
-- -- -- -- |	//GPE
-- -- -- -- |	//GPF
-- -- -- -- |	//GPG
-- -- -- -- |	//GPH
-- -- -- -- |	//ALUM
-- -- -- -- |	//ALUA
-- -- -- -- |	//ALUB
-- -- -- -- |	//FPUM
-- -- -- -- |	//FPUA
-- -- -- -- |	//FPUB
-- -- -- -- |	//RBASE
-- -- -- -- |	//ROFST
-- -- -- -- |	//COMPA
-- -- -- -- |	//COMPB
-- -- -- -- |	//LINK
-- -- -- -- |	//frame pointer
-- -- -- -- |	/////////////////////////////////////////////////
-- -- -- -- |	
-- -- -- -- |	//clear ALU
00 00 07 76 |	ALUM DMEM00
00 00 07 78 |	ALUA DMEM04
00 00 07 7A |	ALUB DMEM08
-- -- -- -- |	
-- -- -- -- |	//clear GPA to store target address
00 00 07 7C |	GPA DMEM0C
-- -- -- -- |	
-- -- -- -- |	//set up ALU for popping target function address
00 00 07 7E |	03 ALUM //subtract mode
00 00 07 80 |	WOFST ALUA
00 00 07 82 |	04 ALUB //4 bytes per register
-- -- -- -- |	
-- -- -- -- |	
-- -- -- -- |	// STACK ////////////////////////////////////////
-- -- -- -- |	// ... local variables      <--- FP
-- -- -- -- |	// arg2
-- -- -- -- |	// arg1
-- -- -- -- |	// arg0
-- -- -- -- |	// target function address
-- -- -- -- |	//                          <--- SP
-- -- -- -- |	//WBASE currently holds caller FP
-- -- -- -- |	//WOFST currently holds caller SP
-- -- -- -- |	
-- -- -- -- |	//read top of stack: target function address
00 00 07 84 |	ALUR WOFST //SP = SP - 4
00 00 07 86 |	WMEM GPA
-- -- -- -- |	
-- -- -- -- |	//restore SP to top of stack
00 00 07 88 |	ALUA WOFST //SP = SP
-- -- -- -- |	
-- -- -- -- |	//set up ALU for stacking
00 00 07 8A |	01 ALUM //add mode
-- -- -- -- |	
-- -- -- -- |	//push caller save registers onto stack
00 00 07 8C |	DMEM0C WMEM //GPA was stored in DMEM earlier
00 00 07 8E |	ALUR WOFST
00 00 07 90 |	ALUR ALUA
00 00 07 92 |	GPB WMEM
00 00 07 94 |	ALUR WOFST
00 00 07 96 |	ALUR ALUA
00 00 07 98 |	GPC WMEM
00 00 07 9A |	ALUR WOFST
00 00 07 9C |	ALUR ALUA
00 00 07 9E |	GPD WMEM
00 00 07 A0 |	ALUR WOFST
00 00 07 A2 |	ALUR ALUA
00 00 07 A4 |	GPE WMEM
00 00 07 A6 |	ALUR WOFST
00 00 07 A8 |	ALUR ALUA
00 00 07 AA |	GPF WMEM
00 00 07 AC |	ALUR WOFST
00 00 07 AE |	ALUR ALUA
00 00 07 B0 |	GPG WMEM
00 00 07 B2 |	ALUR WOFST
00 00 07 B4 |	ALUR ALUA
00 00 07 B6 |	GPH WMEM
00 00 07 B8 |	ALUR WOFST
00 00 07 BA |	ALUR ALUA
-- -- -- -- |	
-- -- -- -- |	//ALU was stored in DMEM temporarily
00 00 07 BC |	DMEM00 WMEM
00 00 07 BE |	ALUR WOFST
00 00 07 C0 |	ALUR ALUA
00 00 07 C2 |	DMEM04 WMEM
00 00 07 C4 |	ALUR WOFST
00 00 07 C6 |	ALUR ALUA
00 00 07 C8 |	DMEM08 WMEM
00 00 07 CA |	ALUR WOFST
00 00 07 CC |	ALUR ALUA
-- -- -- -- |	
00 00 07 CE |	FPUM WMEM
00 00 07 D0 |	ALUR WOFST
00 00 07 D2 |	ALUR ALUA
00 00 07 D4 |	FPUA WMEM
00 00 07 D6 |	ALUR WOFST
00 00 07 D8 |	ALUR ALUA
00 00 07 DA |	FPUB WMEM
00 00 07 DC |	ALUR WOFST
00 00 07 DE |	ALUR ALUA
-- -- -- -- |	
00 00 07 E0 |	RBASE WMEM
00 00 07 E2 |	ALUR WOFST
00 00 07 E4 |	ALUR ALUA
00 00 07 E6 |	ROFST WMEM
00 00 07 E8 |	ALUR WOFST
00 00 07 EA |	ALUR ALUA
-- -- -- -- |	
00 00 07 EC |	COMPA WMEM
00 00 07 EE |	ALUR WOFST
00 00 07 F0 |	ALUR ALUA
00 00 07 F2 |	COMPB WMEM
00 00 07 F4 |	ALUR WOFST
00 00 07 F6 |	ALUR ALUA
-- -- -- -- |	
00 00 07 F8 |	LINK WMEM
00 00 07 FA |	ALUR WOFST
00 00 07 FC |	ALUR ALUA
-- -- -- -- |	
-- -- -- -- |	//push frame pointer on to stack
00 00 07 FE |	WBASE WMEM
00 00 08 00 |	ALUR WOFST
-- -- -- -- |	
-- -- -- -- |	//add a stack frame
00 00 08 02 |	WBASE ALUA
00 00 08 04 |	WOFST ALUB
00 00 08 06 |	ALUR WBASE
00 00 08 08 |	00 WOFST
-- -- -- -- |	
00 00 08 0A |	SKIP LINK
00 00 08 0C |	GPA PC
00 00 08 0E |	00 00 //NOP so SKIP points to the correct address
-- -- -- -- |	
-- -- -- -- |	//return from target function
-- -- -- -- |	
-- -- -- -- |	//retrieve frame pointer from stack
-- -- -- -- |	//subtract 4 from the current frame pointer to
-- -- -- -- |	//get the last item on the stack (the last FP)
00 00 08 10 |	WBASE ALUA
00 00 08 12 |	04 ALUB
00 00 08 14 |	03 ALUM //subtract mode
00 00 08 16 |	ALUR WBASE //set WBASE to last stack slot
00 00 08 18 |	00 WOFST //(clear WOFST)
00 00 08 1A |	WMEM WBASE //FP is the last thing in the stack
-- -- -- -- |	
00 00 08 1C |	ALUR ALUA //ALUR still holds the top stack slot, equivalent to (old FP + old SP)
00 00 08 1E |	WBASE ALUB
-- -- -- -- |	//subtract last FP (WBASE) from last FP + SP (ALUR) to get last SP
00 00 08 20 |	ALUR WOFST //store last SP in WOFST
-- -- -- -- |	//WBASE now holds old FP and WOFST now holds old SP
-- -- -- -- |	//FP and SP are now current
-- -- -- -- |	
-- -- -- -- |	//set up ALU for unstacking
00 00 08 22 |	WOFST ALUA //FP to ALU
00 00 08 24 |	04 ALUB //ALU is in -4 mode
-- -- -- -- |	
-- -- -- -- |	//pop last FP from the stack, don't store because it's already in WBASE
00 00 08 26 |	ALUR WOFST
00 00 08 28 |	ALUR ALUA
-- -- -- -- |	
-- -- -- -- |	//pop caller save registers from stack
00 00 08 2A |	WMEM LINK
00 00 08 2C |	ALUR WOFST
00 00 08 2E |	ALUR ALUA
-- -- -- -- |	
00 00 08 30 |	WMEM COMPB
00 00 08 32 |	ALUR WOFST
00 00 08 34 |	ALUR ALUA
00 00 08 36 |	WMEM COMPA
00 00 08 38 |	ALUR WOFST
00 00 08 3A |	ALUR ALUA
-- -- -- -- |	
00 00 08 3C |	WMEM ROFST
00 00 08 3E |	ALUR WOFST
00 00 08 40 |	ALUR ALUA
00 00 08 42 |	WMEM RBASE
00 00 08 44 |	ALUR WOFST
00 00 08 46 |	ALUR ALUA
-- -- -- -- |	
00 00 08 48 |	WMEM FPUB
00 00 08 4A |	ALUR WOFST
00 00 08 4C |	ALUR ALUA
00 00 08 4E |	WMEM FPUA
00 00 08 50 |	ALUR WOFST
00 00 08 52 |	ALUR ALUA
00 00 08 54 |	WMEM FPUM
00 00 08 56 |	ALUR WOFST
00 00 08 58 |	ALUR ALUA
-- -- -- -- |	
-- -- -- -- |	//store ALU in DMEM while we're still using it
00 00 08 5A |	WMEM DMEM08 //ALUB
00 00 08 5C |	ALUR WOFST
00 00 08 5E |	ALUR ALUA
00 00 08 60 |	WMEM DMEM04 //ALUA
00 00 08 62 |	ALUR WOFST
00 00 08 64 |	ALUR ALUA
00 00 08 66 |	WMEM DMEM00 //ALUM
00 00 08 68 |	ALUR WOFST
00 00 08 6A |	ALUR ALUA
-- -- -- -- |	
00 00 08 6C |	WMEM GPH
00 00 08 6E |	ALUR WOFST
00 00 08 70 |	ALUR ALUA
00 00 08 72 |	WMEM GPG
00 00 08 74 |	ALUR WOFST
00 00 08 76 |	ALUR ALUA
00 00 08 78 |	WMEM GPF
00 00 08 7A |	ALUR WOFST
00 00 08 7C |	ALUR ALUA
00 00 08 7E |	WMEM GPE
00 00 08 80 |	ALUR WOFST
00 00 08 82 |	ALUR ALUA
00 00 08 84 |	WMEM GPD
00 00 08 86 |	ALUR WOFST
00 00 08 88 |	ALUR ALUA
00 00 08 8A |	WMEM GPC
00 00 08 8C |	ALUR WOFST
00 00 08 8E |	ALUR ALUA
00 00 08 90 |	WMEM GPB
00 00 08 92 |	ALUR WOFST
00 00 08 94 |	ALUR ALUA
00 00 08 96 |	WMEM GPA
-- -- -- -- |	
-- -- -- -- |	//finished with ALU, restore it from DMEM
-- -- -- -- |	//TODO this can be made slightly more efficient by just doing ALU last
00 00 08 98 |	DMEM00 ALUM
00 00 08 9A |	DMEM04 ALUA
00 00 08 9C |	DMEM08 ALUB
-- -- -- -- |	
-- -- -- -- |	//return to the caller
00 00 08 9E |	LINK PC
-- -- -- -- |	/////////////////////////////////////////////////
